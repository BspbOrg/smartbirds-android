def versionNameBase = "2.0.6"
def versionCodeBase = 76


apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.novoda.build-properties'


buildProperties {
    notThere {
        using rootProject.file('debug_gradle.properties')
    }
    secrets {
        using rootProject.file('keystore.properties')
    }
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "org.bspb.smartbirds.pro"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode versionCodeBase
        versionName "${versionNameBase}"

        def buildTime = new GregorianCalendar().format("MM-dd-yyyy' 'HH:mm:ss Z")
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }
    signingConfigs {
        release {
            keyAlias((buildProperties.secrets['keyAlias'] | buildProperties.notThere['keyAlias']).string)
            keyPassword((buildProperties.secrets['keyPassword'] | buildProperties.notThere['keyPassword']).string)
            storeFile file((buildProperties.secrets['storeFile'] | buildProperties.notThere['storeFile']).string)
            storePassword((buildProperties.secrets['storePassword'] | buildProperties.notThere['storePassword']).string)
        }
    }
    buildTypes {
        ext.betaDistributionGroupAliases = "BSPB"

        debug {
            versionNameSuffix "-DEBUG"
            buildConfigField "String", "GIT_SHA", "\"DEBUG\""
            buildConfigField "String", "BACKEND_BASE_URL", "\"https://staging.smartbirds.org/api/staging/\""
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
            buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
            buildConfigField "String", "BACKEND_BASE_URL", "\"https://smartbirds.org/api/\""

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFileName = "smartbirdspro-${variant.versionName}-${versionCodeBase}.apk"
                }
            }
        }
    }
    lintOptions {
        abortOnError true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    kapt {
        correctErrorTypes = true
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt "org.androidannotations:androidannotations:4.7.0"
    implementation "org.androidannotations:androidannotations-api:4.8.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // KTX extensions
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation "androidx.fragment:fragment-ktx:1.2.5"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.android.gms:play-services-maps:17.0.0'

    // TODO: migrate to v3.x
    implementation 'de.greenrobot:eventbus:2.4.1'

    // Crashlytics Kit
    implementation 'com.google.firebase:firebase-crashlytics:17.3.0'

    implementation 'com.google.firebase:firebase-analytics:18.0.0'

    // TODO: migrate to https://commons.apache.org/proper/commons-csv/
    implementation 'com.googlecode.jcsv:jcsv:1.4.0'
    implementation 'commons-net:commons-net:3.7.2'
    implementation 'com.google.maps.android:android-maps-utils:2.1.0'

    implementation 'org.osmdroid:osmdroid-android:6.1.8'
    implementation 'com.github.MKergall:osmbonuspack:6.6.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'

    // http://square.github.io/retrofit/
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:4.9.0'

    // https://github.com/SimonVT/schematic
    kapt 'net.simonvt.schematic:schematic-compiler:0.7.0'
    implementation 'net.simonvt.schematic:schematic:0.7.0'

    implementation "androidx.lifecycle:lifecycle-common-java8:2.2.0"
}
