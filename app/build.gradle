def versionNameBase = "1.2.1"
def versionCodeBase = 29


apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 24
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId "org.bspb.smartbirds.pro"
        minSdkVersion 15
        // keep 23 until we resolve the FileUriExposedException on api 24+
        // @see http://stackoverflow.com/questions/32623304/when-take-photo-get-java-lang-throwable-file-uri-exposed-through-clipdata
        targetSdkVersion 23
        versionCode versionCodeBase
        versionName "${versionNameBase}"

        def buildTime = new GregorianCalendar().format("MM-dd-yyyy' 'HH:mm:ss Z")
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }
    signingConfigs {
        release {
            storeFile file("../release.jks")
            storePassword "***REMOVED***"
            keyAlias "smartbirds-pro"
            keyPassword "***REMOVED***"
        }
    }
    buildTypes {
        ext.betaDistributionGroupAliases = "BSPB"

        debug {
            versionNameSuffix "-DEBUG"
            buildConfigField "String", "GIT_SHA", "\"DEBUG\""
            buildConfigField "String", "BACKEND_BASE_URL", "\"http://sb.87.252.173.51.xip.io:8082/api/\""
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
            buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
            buildConfigField "String", "BACKEND_BASE_URL", "\"https://smartbirds.org/api/\""

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFile = new File(output.outputFile.parent, "smartbirdspro-${variant.versionName}-${versionCodeBase}.apk")
                }
            }

        }
    }
    lintOptions {
        abortOnError false
    }
}

apt {
    arguments {
        resourcePackageName android.defaultConfig.applicationId
        androidManifestFile variant.outputs[0]?.processResources?.manifestFile
    }
}

dependencies {
    apt "org.androidannotations:androidannotations:4.0.0"
    compile "org.androidannotations:androidannotations-api:4.0.0"

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.android.gms:play-services-maps:9.6.1'
    compile 'de.greenrobot:eventbus:2.4.1'
    // Crashlytics Kit
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true
    }
    compile 'com.googlecode.jcsv:jcsv:1.4.0'
    compile 'commons-net:commons-net:3.3'
    compile 'com.google.maps.android:android-maps-utils:0.3.2'
    compile 'org.osmdroid:osmdroid-android:4.2'
    compile 'com.android.support:design:24.2.1'
    compile "com.android.support:support-v13:24.2.1"

    // http://square.github.io/retrofit/
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.4.1'

    // https://github.com/SimonVT/schematic
    apt 'net.simonvt.schematic:schematic-compiler:0.6.3'
    compile 'net.simonvt.schematic:schematic:0.6.7'
}
