def versionNameBase = "1.2.22"
def versionCodeBase = 50


apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "org.bspb.smartbirds.pro"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode versionCodeBase
        versionName "${versionNameBase}"

        def buildTime = new GregorianCalendar().format("MM-dd-yyyy' 'HH:mm:ss Z")
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }
    signingConfigs {
        release {
            storeFile file("../release.jks")
            keyAlias "smartbirds-pro"
        }
    }
    buildTypes {
        ext.betaDistributionGroupAliases = "BSPB"

        debug {
            versionNameSuffix "-DEBUG"
            buildConfigField "String", "GIT_SHA", "\"DEBUG\""
            buildConfigField "String", "BACKEND_BASE_URL", "\"http://sb.87.252.173.51.xip.io:8082/api/\""
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
            buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
            buildConfigField "String", "BACKEND_BASE_URL", "\"https://smartbirds.org/api/\""

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFileName = "smartbirdspro-${variant.versionName}-${versionCodeBase}.apk"
                }
            }

        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    annotationProcessor "org.androidannotations:androidannotations:4.4.0"
    implementation "org.androidannotations:androidannotations-api:4.4.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    // TODO: migrate to v3.x
    implementation 'de.greenrobot:eventbus:2.4.1'
    // Crashlytics Kit
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true
    }
    // TODO: migrate to https://commons.apache.org/proper/commons-csv/
    implementation 'com.googlecode.jcsv:jcsv:1.4.0'
    implementation 'commons-net:commons-net:3.3'
    implementation 'com.google.maps.android:android-maps-utils:0.3.2'
    // TODO: migrate to v6.x
    implementation 'org.osmdroid:osmdroid-android:4.2'
    implementation 'com.android.support:design:28.0.0'
    implementation "com.android.support:support-v13:28.0.0"

    // http://square.github.io/retrofit/
    implementation 'com.squareup.retrofit2:retrofit:2.7.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.4.1'

    // https://github.com/SimonVT/schematic
    // TODO: migrate to v0.7.x
    annotationProcessor 'net.simonvt.schematic:schematic-compiler:0.6.3'
    implementation 'net.simonvt.schematic:schematic:0.6.7'
}
