def versionNameBase = "1.2.23"
def versionCodeBase = 51


apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def keystorePropertiesFile = rootProject.file('keystore.properties')
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "org.bspb.smartbirds.pro"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode versionCodeBase
        versionName "${versionNameBase}"

        def buildTime = new GregorianCalendar().format("MM-dd-yyyy' 'HH:mm:ss Z")
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            keyAlias keystoreProperties['keyAlias']
            storePassword keystoreProperties['storePassword']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    buildTypes {
        ext.betaDistributionGroupAliases = "BSPB"

        debug {
            versionNameSuffix "-DEBUG"
            buildConfigField "String", "GIT_SHA", "\"DEBUG\""
            buildConfigField "String", "BACKEND_BASE_URL", "\"https://staging.smartbirds.org/api/staging/\""
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
            buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
            buildConfigField "String", "BACKEND_BASE_URL", "\"https://smartbirds.org/api/\""

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFileName = "smartbirdspro-${variant.versionName}-${versionCodeBase}.apk"
                }
            }

        }
    }
    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    annotationProcessor "org.androidannotations:androidannotations:4.6.0"
    implementation "org.androidannotations:androidannotations-api:4.6.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.android.gms:play-services-maps:17.0.0'

    // TODO: migrate to v3.x
    implementation 'de.greenrobot:eventbus:2.4.1'

    // Crashlytics Kit
    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true
    }
    // TODO: migrate to https://commons.apache.org/proper/commons-csv/
    implementation 'com.googlecode.jcsv:jcsv:1.4.0'
    implementation 'commons-net:commons-net:3.6'
    implementation 'com.google.maps.android:android-maps-utils:1.1.0'
    // TODO: migrate to v6.x
    implementation 'org.osmdroid:osmdroid-android:4.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'

    // http://square.github.io/retrofit/
    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.4.1'

    // https://github.com/SimonVT/schematic
    // TODO: migrate to v0.7.x
    annotationProcessor 'net.simonvt.schematic:schematic-compiler:0.7.0'
    implementation 'net.simonvt.schematic:schematic:0.7.0'
}
